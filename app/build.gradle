plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.compose.compiler)
}

android {
    namespace 'com.richardluo.globalIconPack'
    compileSdk 35

    def versionProps = new Properties()
    versionProps.load(new FileInputStream("$project.rootDir/version.properties"))
    int major = versionProps['majorVersion'] as Integer
    int minor = versionProps['minorVersion'] as Integer
    int patch = versionProps['patchVersion'] as Integer

    defaultConfig {
        applicationId "com.richardluo.globalIconPack"
        minSdk 33
        targetSdk 35
        versionCode major * 10000 + minor * 100 + patch
        versionName "${major}.${minor}.${patch}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    def keystoreProps = new Properties()
    keystoreProps.load(new FileInputStream("$project.rootDir/keystore.properties"))
    signingConfigs {
        release {
            keyAlias keystoreProps['keyAlias']
            keyPassword keystoreProps['keyPassword']
            storeFile file(keystoreProps['storeFile'])
            storePassword keystoreProps['storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debugApp {
            debuggable true
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        buildConfig true
        compose true
    }
    dependenciesInfo {
        // Disables dependency metadata when building APKs.
        includeInApk = false
        // Disables dependency metadata when building Android App Bundles.
        includeInBundle = false
    }
}

dependencies {
//    testImplementation libs.junit
//    androidTestImplementation libs.ext.junit
//    androidTestImplementation libs.espresso.core
    implementation libs.core.ktx
    implementation libs.activity
    implementation libs.preference
    implementation libs.material3
    implementation libs.libsu

    compileOnly(fileTree("libs") { include("api-82.jar") })
}